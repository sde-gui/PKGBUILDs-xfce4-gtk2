# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

_pkgbase=garcon
pkgname=(${_pkgbase}-gtk2)
pkgver=0.4.0
pkgrel=1
pkgdesc="Implementation of the freedesktop.org menu specification"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('LGPL')
groups=('xfce4-gtk2')
conflicts=("${_pkgbase}")
provides=("${_pkgbase}")
depends=('libxfce4ui-gtk2')
makedepends=('intltool')
replaces=('libxfce4menu')
source=(http://archive.xfce.org/src/xfce/${_pkgbase}/${pkgver%.*}/${_pkgbase}-$pkgver.tar.bz2)
sha256sums=('787dc859713b6518992d760d4b858fb02e8a30705e6f6e871d7a14cc97bca972')

build() {
  cd "$srcdir/${_pkgbase}-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/${_pkgbase}-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
