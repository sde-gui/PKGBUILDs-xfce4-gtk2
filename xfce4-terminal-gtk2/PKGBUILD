# Maintainer: Newb I the Newbd <czbd hostedon o2 halfacolon pl>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

_pkgbase=xfce4-terminal
pkgname=(${_pkgbase}-gtk2)
pkgver=0.6.3
pkgrel=1
pkgdesc="A modern terminal emulator primarly for the Xfce desktop environment (GTK2 version)"
arch=('x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk2')
conflicts=("${_pkgbase}")
provides=("${_pkgbase}")
depends=('libxfce4ui-gtk2' 'vte' 'hicolor-icon-theme')
makedepends=('intltool')
source=(http://archive.xfce.org/src/apps/${_pkgbase}/${pkgver%.*}/${_pkgbase}-${pkgver}.tar.bz2)
sha256sums=('912f4716c2395a14a80620ef982b4af1e2a67a8df9a1ef0b802ecae826057e08')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/xfce4 \
        --localstatedir=/var \
        --disable-static \
        --disable-debug
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
